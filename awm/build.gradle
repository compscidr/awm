group 'io.rightmesh'
version '1.1.23' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    buildToolsVersion "28.0.3"
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 23
        versionName this.version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.all {

        }
    }
}

dependencies {
    //permissions
    implementation 'com.vanniktech:rxpermission:0.4.0'
    api 'com.github.anadea:rxbus:1.0.1'

    //concurrent set
    implementation 'com.google.guava:guava:27.0.1-android'

    //play services for location
    implementation ('com.google.android.gms:play-services-location:18.0.0') {
        exclude group: "com.android.support"
    }

    //lombok annotations
    compileOnly 'org.projectlombok:lombok:1.18.20'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    //unit and integration tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.1"
    testImplementation "org.mockito:mockito-core:3.9.0"

    //persistence
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'

    //logging
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
}

repositories {
    google()
    mavenLocal()
    mavenCentral()
    jcenter()
}


task getVersion() {
    print version
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = group
                artifactId = 'awm'
                version = version
            }
            debug(MavenPublication) {
                from components.debug
                groupId = group
                artifactId = 'awm-debug'
                version = version
            }
        }
    }
    task updatePom(type: Copy) {
        from "$buildDir/publications/release/pom-default.xml"
        into "."
        rename ("pom-default.xml", "pom.xml")
        dependsOn(generatePomFileForReleasePublication)
    }
    build.dependsOn(updatePom)
}