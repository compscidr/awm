// note: for this to work, we need to apply both the jacoco and the jacoco-android plugin

// originally wanted to use this: https://github.com/arturdm/jacoco-android-gradle-plugin
// but it no longer works with gradle >= 6.0
// see: https://github.com/arturdm/jacoco-android-gradle-plugin/issues
// so we're using this fork: https://github.com/autonomousapps/jacoco-android-gradle-plugin
jacoco {
    toolVersion = '0.8.5' // type String
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    // https://github.com/gradle/gradle/issues/5184
    jacoco.excludes = ['jdk.internal.*']
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
    destination "build/reports/jacoco"
    excludes += [
            '**/**Hilt**.class',
    ]
}

// https://github.com/autonomousapps/jacoco-android-gradle-plugin/issues/1
afterEvaluate {
    def task = tasks.getByName("jacocoTestDebugUnitTestReport") as JacocoReportBase
    def tree = fileTree(buildDir)
    tree.include("**/*.ec")
    task.executionData(tree)
}
